config {
    type: "table",
    dependencies: ["search_phrases", "check_text_chunks"],
}

js {
  const { getCurrentDatasetId } = require("includes/utils.js")
  const dataset_id = utils.getCurrentDatasetId();
  const dimension = dataform.projectConfig.vars.embedding_dimensionality;
}

SELECT
  *,
  'text-embedding-005/768/round1' AS tags
FROM ML.GENERATE_EMBEDDING (
  MODEL  `${dataset_id}.text-embedding`,
  (
    SELECT cik, accession_number, chunk_num, content
    FROM `${dataset_id}.filing_text_chunks`
  ),
  STRUCT(
    TRUE AS flatten_json_output,
    ${dimension} AS output_dimensionality,
    'RETRIEVAL_DOCUMENT' AS task_type
  )
)

post_operations {
      CREATE VECTOR INDEX IF NOT EXISTS embedding_index 
      ON ${self()} (ml_generate_embedding_result) 
      OPTIONS (
        index_type = "TREE_AH",
        distance_type = "COSINE",
        tree_ah_options = '{"leaf_node_embedding_count": 500 }'
      )
}